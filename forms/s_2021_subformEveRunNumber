Option Compare Database

Private Sub HybID_DblClick(Cancel As Integer)
''Given a run rumber find the PGD referrals on that run

    'define variables for files
    Dim EvEInputFile As String
    Dim EvEinputFileFolder As String
    Dim EvEInputFilePath As String
        
    'set file variables for EvE
    EvEInputFile = Me.ArrayRunNumber & "EvE.txt"
    EvEinputFileFolder = "S:\Genetics_Data2\Array\Audits and Projects\150702 PGD FEfiles\frontend\Test"
    EvEInputFilePath = EvEinputFileFolder & "\" & EvEInputFile
    
    'define variables for sql query
    Dim Sql As String
    Dim RunID As String
      
    'define runID
    RunID = CStr(Me.ArrayRunNumber)
    ' set query - union query that captures the barcode, subarray and label, and the patients first and last names and creates the hyb ID by joining the required tables and filtering for the run ID
    ' removes any sureplex samples and selects for PGD referral IDs. The union is required to repeat for cy3 and cy5 DNAIDs
    Sql = " SELECT Arrays.ArrayBarCode, ArrayLabelling.Subarray, ArrayLabelling.ArrayRunNumber, ArrayLabel.EveLabel, Patients.BookinFirstName, " _
    & " Patients.BookinLastName, right(Arrays.ArrayBarCode,3)&"".""&ArrayLabelling.Subarray&"" (""&ArrayLabelling.ArrayRunNumber&"")"" as HybID " _
    & " FROM Patients INNER JOIN ((ArrayLabelledDNA INNER JOIN ArrayLabel ON ArrayLabelledDNA.ArrayLabelID = ArrayLabel.LabelID) INNER JOIN " _
    & " ((ArrayLabelling INNER JOIN Arrays ON ArrayLabelling.ArrayID = Arrays.ArrayID) INNER JOIN ((ArrayTest INNER JOIN " _
    & " Referral ON ArrayTest.ReferralID = Referral.ReferralID) INNER JOIN DNA ON ArrayTest.InternalPatientID = DNA.InternalPatientID) " _
    & " ON ArrayLabelling.Cy5DNAID = DNA.DNAID) ON ArrayLabelledDNA.DNAID = DNA.DNAID) ON Patients.InternalPatientID = DNA.InternalPatientID " _
    & " WHERE (((ArrayLabelling.ArrayRunNumber)='" & RunID & "') AND ((Patients.BookinLastName)<>""SUREPLEX"") AND ((Referral.ReferralID) In " _
    & " (1199901203,1199901190,1199901193,1199901206,1199901207))) UNION SELECT Arrays.ArrayBarCode, ArrayLabelling.Subarray, ArrayLabelling.ArrayRunNumber, ArrayLabel.EveLabel, " _
    & " Patients.BookinFirstName, Patients.BookinLastName, right(Arrays.ArrayBarCode,3)&"".""&ArrayLabelling.Subarray&"" (""&ArrayLabelling.ArrayRunNumber&"")"" as HybID " _
    & " FROM Patients INNER JOIN ((ArrayLabelledDNA INNER JOIN ArrayLabel ON ArrayLabelledDNA.ArrayLabelID = ArrayLabel.LabelID) INNER JOIN ((ArrayLabelling " _
    & " INNER JOIN Arrays ON ArrayLabelling.ArrayID = Arrays.ArrayID)INNER JOIN ((ArrayTest INNER JOIN Referral ON ArrayTest.ReferralID = " _
    & " Referral.ReferralID) INNER JOIN DNA ON ArrayTest.InternalPatientID = DNA.InternalPatientID) ON ArrayLabelling.Cy3DNAID = DNA.DNAID) " _
    & " ON ArrayLabelledDNA.DNAID = DNA.DNAID) ON Patients.InternalPatientID = DNA.InternalPatientID " _
    & " WHERE (((ArrayLabelling.ArrayRunNumber)= '" & RunID & "') AND ((Patients.BookinLastName)<>""SUREPLEX"") AND ((Referral.ReferralID) In (1199901203,1199901190,1199901193,1199901206,1199901207))); "
             
                    
    ' define variable used in the eve input file
    Dim MagicEmbryo As String
    MagicEmbryo = "252846911966" & vbTab & "7" & vbTab & "Cy5" & vbTab
    
    'set the variables to write the file
    Set fs = CreateObject("Scripting.FileSystemObject")
    Set a = fs.CreateTextFile(EvEInputFilePath, False) 'False will error if the input file has already been created
    
    ' variable for the query
    Dim Rst As DAO.Recordset
    
    'run the query
    Set Rst = CurrentDb.OpenRecordset(Sql, dbOpenSnapshot)
    
    'loop through query result and write each line using the magic embryo info for Cy3 and the query result as Cy5
    Do While Not Rst.EOF
        a.WriteLine (MagicEmbryo & Rst!ArrayBarCode & vbTab & Rst!Subarray & vbTab & Rst!EveLabel)
        Rst.MoveNext
    Loop
    Rst.Close
    Set Rst = Nothing
    
    ''Call eve
    'Create shell object
    Dim wsh As Object
    Set wsh = VBA.CreateObject("WScript.Shell")
    
    ' settings to have cmd box visible while running
    Dim waitOnReturn As Boolean: waitOnReturn = True
    Dim windowStyle As Integer: windowStyle = 1
    
    'allow capture of error code
    Dim ErrorCode As Integer
    
    ' define the variables to feed to EvE
    Dim EvePath As String
    Dim OutputDir As String
    Dim TotalCmd As String
    
    'set path to eve
    EvePath = "S:\Genetics_Data2\Array\Audits and Projects\150702 PGD FEfiles\aled_DLRS\EvE.py"
    
    'set folder to put newly created FE files
    OutputDir = "S:\Genetics_Data2\Array\Audits and Projects\150702 PGD FEfiles\frontend\Test\eveoutput"
    
    'set the commands to run eve (eve.py, input file and output dir) taking into account the quotations etc needed due to spaces in file paths
    TotalCmd = """" & EvePath & """" & " " & """" & EvEInputFilePath & """" & " " & """" & OutputDir & """"
    
    ' run command NB /K can be used for the cmd line window to remain after it has run - good for debugging
    ErrorCode = wsh.Run("cmd.exe /S /K S:\Genetics_Data2\Array\Software\Python\python.exe " & TotalCmd, windowStyle, waitOnReturn)
    
    'report error messages
    If ErrorCode = 0 Then
        MsgBox "EvE was run successfully. FE files can be found at " & OutputDir
    Else
        MsgBox "Program exited with error code " & ErrorCode & "."
    End If
End Sub
